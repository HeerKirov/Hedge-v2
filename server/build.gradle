plugins {
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'com.github.johnrengelman.shadow' version '4.0.3'
    id 'org.beryx.jlink' version '2.22.1'
}

group 'com.heerkirov.hedge.server'
version '0.1.0'

sourceCompatibility = '11'
targetCompatibility = '11'

mainClassName = 'com.heerkirov.hedge.server/com.heerkirov.hedge.server.ApplicationKt'

application {
    mainClass = "com.heerkirov.hedge.server.ApplicationKt"
    mainModule = "com.heerkirov.hedge.server"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation group: "org.jetbrains.kotlin", name: "kotlin-stdlib-jdk8", version: "1.4.10"
    implementation group: "org.jetbrains.kotlin", name: "kotlin-reflect", version: "1.4.10"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.3'
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.11.3'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.32.3.2'
    implementation group: 'me.liuwj.ktorm', name: 'ktorm-core', version: '3.1.0'
    implementation group: 'me.liuwj.ktorm', name: 'ktorm-support-sqlite', version: '3.1.0'
}

compileJava {
    doFirst {
        options.compilerArgs = [
            '--module-path', classpath.asPath
        ]
    }
}
compileKotlin { kotlinOptions.jvmTarget = '11' }
compileTestKotlin { kotlinOptions.jvmTarget = '11' }

shadowJar {
    manifestContentCharset 'utf-8'
    metadataCharset 'utf-8'
    manifest {
        attributes 'Main-Class': 'com.heerkirov.hedge.server.ApplicationKt'
    }
}

jlink {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        name = "hedge-v2-server"
    }
    mergedModule {
        additive = true
        /* 存在一个jlink插件auto merge non-module代码时的问题。
         * 新的java版本采用uses在module中声明服务发现的implement。对于non-module的代码，jlink插件会自动把它们打包成一个merged-module。
         * 但是，kotlin-reflect内有一些ServiceLoader的uses没有被自动分析进merged module里，因此造成了一个module * does not declare `uses`的异常。
         * tips: 在additive=true时，不必携带全部DSL，jlink命令的:createMergedModule阶段仍会报warn，但不会影响构建。
         */
        uses 'kotlin.reflect.jvm.internal.impl.resolve.ExternalOverridabilityCondition'
        uses 'kotlin.reflect.jvm.internal.impl.util.ModuleVisibilityHelper'
    }
}